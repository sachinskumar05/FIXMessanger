apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

def generateSourceDir = new File("${project.buildDir}/gen-src/main/java")

version             = '2.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 25
    options.compilerArgs += ["--enable-preview"]
}

application {
    mainClass = "com.sachin.qfixmessenger.FixMessenger"
    applicationDefaultJvmArgs = ["--enable-preview"]
}

repositories {
    mavenCentral()
}

configurations {
    xjc
}

dependencies {

    implementation 'org.agrona:agrona:2.4.0'

    xjc 'com.sun.xml.bind:jaxb-xjc:4.0.0'
    xjc 'com.sun.xml.bind:jaxb-core:4.0.0'
    xjc 'com.sun.xml.bind:jaxb-impl:4.0.0'
    xjc 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    xjc 'jakarta.activation:jakarta.activation-api:2.1.2'

    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.0' // Upgraded to match
    implementation 'com.sun.xml.bind:jaxb-core:4.0.0'

    implementation 'org.quickfixj:quickfixj-all:2.3.1'

    implementation 'jdom:jdom:1.1'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.apache.mina:mina-core:2.1.6'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-log4j12:1.7.36'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.0'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'

    implementation     fileTree(dir: 'lib/compile', include: '*.jar')
}

sourceSets {
    main {
        java {
            srcDir generateSourceDir
        }
    }
}

task createDirs {
    doLast {
        generateSourceDir.mkdirs()
    }
}

tasks.register('xjc', JavaExec) {
    dependsOn createDirs
    
    mainClass = 'com.sun.tools.xjc.XJCFacade'
    classpath = configurations.xjc
    args = ['-d', generateSourceDir, '-extension'] + fileTree(dir: 'src/main/xsd', include: '*.xsd').files
    
    doFirst {
        logger.lifecycle("Running XJC...")
    }
}


task generateSource {
    dependsOn 'xjc'
}

clean.doLast {
    ant.delete(dir: 'log')
    ant.delete(dir: 'data')
}

cleanEclipse.doLast {
    ant.delete(dir: 'bin')
}

compileJava.dependsOn generateSource

test {
    useJUnitPlatform()
    jvmArgs("--enable-preview")
}

jar {
    manifest {
        attributes 'Implementation-Title': 'QuickFIX Messenger', 'Implementation-Version': version
    }
}

distributions {
    main {
        contents {
            from('src/main/cfg') {
                into 'cfg'
            }

            from('src/main/cfg/log4j.properties') {
                into 'lib/log4j'
            }

            from('src/main/dictionary') {
                into 'lib/dictionary'
            }

            from('src/main/scripts') {
                into '.'
            }
        }
    }
}

startScripts {
    classpath += files('$APP_HOME/dictionary')
    classpath += files('$APP_HOME/log4j')
}
